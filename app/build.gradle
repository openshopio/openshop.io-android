apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.1"
    // needed for okHttp from 23sdk. https://medium.com/android-news/android-networking-i-okhttp-volley-and-gson-72004efff196#.gsfpn3ez8
    useLibrary 'org.apache.http.legacy'

    dexOptions {
        javaMaxHeapSize "3g"
    }

    signingConfigs {
        release {
            try {
                storeFile file("openshopKeystore.jks")
                storePassword KEYSTORE_PASSWORD
                keyAlias KEYSTORE_ALIAS
                keyPassword KEY_PASSWORD
            } catch (ex) {
                throw new InvalidUserDataException("You should define KEYSTORE_PASSWORD, KEYSTORE_ALIAS and KEY_PASSWORD in gradle.properties." + ex)
            }
        }
    }

    defaultConfig {
        applicationId "bf.io.openshop"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 92
        versionName "1.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.release
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            versionNameSuffix '-DEBUG'
        }

        release {
            minifyEnabled false
            debuggable false
            signingConfig signingConfigs.release

            // For release build should be added custom proGuard
//            minifyEnabled true
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    flavorDimensions 'tier'

    productFlavors {
        Modern {
            dimension 'tier'
            minSdkVersion 23
        }
        Legacy {
            dimension 'tier'
            minSdkVersion 15
        }
    }

    sourceSets {
        androidTest {
            resources.srcDirs += ['src/androidTest/resources']
        }
    }
}

task clearData(type: Exec) {
    def clearDataCommand = ['adb', 'shell', 'pm', 'clear', 'bf.io.openshop']
    commandLine clearDataCommand
}

repositories { mavenCentral() }

dependencies {
    //  if needed jar libraries, add them to libs folder and load them here:
    //  compile fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0-beta01'
    implementation 'com.android.support:design:28.0.0-beta01'
    implementation 'com.android.support:recyclerview-v7:28.0.0-beta01'
    implementation 'com.jakewharton.timber:timber:4.1.2'
    implementation 'com.google.android.gms:play-services-analytics:16.0.1'
    implementation 'com.google.android.gms:play-services-gcm:15.0.1'
    implementation 'com.google.android.gms:play-services-maps:15.0.1'
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'mbanje.kurt:fabbutton:1.2.1'
    implementation 'com.facebook.android:facebook-android-sdk:4.11.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.squareup.okhttp3:okhttp:3.7.0'
    implementation 'com.android.volley:volley:1.0.0'
    implementation 'com.android.support.test.espresso:espresso-idling-resource:3.0.2'


    //// Unit testing dependencies
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-all:1.10.19'
    // Set this dependency if you want to use the Hamcrest matcher library
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation 'org.powermock:powermock-module-junit4:1.6.4'
    testImplementation 'org.powermock:powermock-api-mockito:1.6.4'
    //// Instrumentation test dependencies
    androidTestImplementation 'com.android.support:support-annotations:27.1.1'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-intents:3.0.2'
}

apply plugin: 'com.google.gms.google-services'




